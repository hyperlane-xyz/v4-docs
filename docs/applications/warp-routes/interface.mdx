---
title: "HWR Interface"
---

Hyperlane Warp Routes (HWR) leverage the `IHypERC20` token interface. HWR tokens implement this interface, which extends the standard `ERC20` interface.

```solidity
/// @notice An interchain extension of the ERC20 interface
interface IHypERC20 is IERC20 {
    /**
     * @notice Transfers tokens to the specified recipient on a remote chain
     * @param _destination The domain ID of the destination chain
     * @param _recipient The address of the recipient, encoded as bytes32
     * @param _amount The amount of tokens to transfer
     */
    function transferRemote(
        uint32 _destination,
        bytes32 _recipient,
        uint256 _amount
    ) external payable;

    /**
     * @notice Quotes the fees required for a remote transfer
     * @param _destination The domain ID of the destination chain
     * @param _recipient The address of the recipient, encoded as bytes32
     * @param _amount The amount of tokens to transfer
     * @return quotes An array of Quote structs representing fees in different tokens
     */
    function quoteTransferRemote(
        uint32 _destination,
        bytes32 _recipient,
        uint256 _amount
    ) external view returns (Quote[] memory quotes);
}

struct Quote {
    address token; // Token address (address(0) for native token)
    uint256 amount; // Fee amount in the specified token
}
```

## Fee Quoting Interface

<Info>
  New in version 10.0.0: The `quoteTransferRemote` function provides a standardized way to quote fees for warp route transfers.
</Info>

### Quote Structure

The `quoteTransferRemote` function returns an array of exactly 3 `Quote` structs with the following convention:

1. **Index 0: Native Mailbox Dispatch Fee**
   - Token: `address(0)` (native token like ETH, MATIC, etc.)
   - Amount: Fee charged by the Hyperlane mailbox for dispatching the message
   - This must be sent as `msg.value` when calling `transferRemote`

2. **Index 1: Internal Token Amount**
   - Token: The warp route token address (from `token()` function)
   - Amount: `_amount + internalFeeAmount`
   - This is the total amount of tokens that must be sent/approved, including any internal fees charged by the warp route

3. **Index 2: External Bridge Fee**
   - Token: The warp route token address (from `token()` function)
   - Amount: Additional fees for external bridges (e.g., CCTP, Everclear), or `0` if none
   - This is included in the total amount required at index 1

### Exact Amount Out Semantics

Warp routes use "exact amount out" semantics, meaning:

- The `_amount` parameter in `transferRemote` specifies the **exact amount the recipient receives**
- Fees are charged **on top** of this amount from the sender
- Total tokens required = `_amount + internalFeeAmount + externalFeeAmount`

**Example:**

```solidity
// Get fee quote
Quote[] memory quotes = warpRoute.quoteTransferRemote(
    destinationDomain,
    recipientAddress,
    1000e18  // Recipient will receive exactly 1000 tokens
);

// quotes[0]: Native fee (e.g., 0.01 ETH)
// quotes[1]: Token amount (e.g., 1005e18 = 1000 + 5 fee)
// quotes[2]: External fee (e.g., 0 or included in quotes[1])

// Approve total token amount
IERC20(warpRoute.token()).approve(
    address(warpRoute),
    quotes[1].amount  // 1005e18 total
);

// Transfer with native fee
warpRoute.transferRemote{value: quotes[0].amount}(
    destinationDomain,
    recipientAddress,
    1000e18  // Recipient receives exactly 1000 tokens
);
```

<Warning>
  **Important:** Always query fees on-chain immediately before the transfer, as fees may change based on gas prices, exchange rates, or route configuration.
</Warning>

## Deploy your HWR

Ready to deploy your HWR? It's easy, follow the [step-by-step guide](/docs/guides/quickstart/deploy-warp-route).
